/*
 * Copyright (c) 2020 Charlie Condorcet Engineer Student.
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * General Public License as published by the Free Software Foundation, either version 3 of the License, or (at
 * your option) any later version.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without
 * even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with this program. If not, see
 * <https://www.gnu.org/licenses/>.
 */

// Install the gradle Ice Builder plug-in from the plug-in portal
plugins {
    // https://plugins.gradle.org/plugin/com.zeroc.gradle.ice-builder.slice
    id 'com.zeroc.gradle.ice-builder.slice' version '1.4.7' apply false

    // https://plugins.gradle.org/plugin/com.github.ben-manes.versions
    id 'com.github.ben-manes.versions' version '0.28.0' apply true
}

// The root project
group 'cl.disc.ucn.pdis'
version '1.0.6'

allprojects {
    // buildDir = "${System.env.TEMP}//gradle-build//${rootProject.name}//${project.name}"
    // logger.lifecycle("Version(${BUILD_VERSION}) BuildDir(${buildDir})")
}

subprojects {

    // Apply Java and Ice Builder plug-ins to all sub-projects
    apply plugin: 'java'
    apply plugin: 'com.zeroc.gradle.ice-builder.slice'

    // Java code version
    sourceCompatibility = 1.8

    // Testing configuration
    test {

        // Enable JUnit Platform (a.k.a. JUnit 5) support
        useJUnitPlatform()

        // failFast = true

        // Run the test in parallel
        systemProperty 'junit.jupiter.execution.parallel.enabled', 'true'
        systemProperty 'junit.jupiter.execution.parallel.mode.default', 'concurrent'
    }

    // Both Client and Server projects share the *.ice Slice definitions
    slice {

        // The home of ice
        iceHome = 'C:\\Program Files\\ZeroC\\Ice-3.7.3\\'

        java {
            files = [file("../slice/domain.ice")]
            // args = "--debug --impl"
            logger.lifecycle("Slicing file: <${files}>")
        }
    }

    // Use Ice JAR files from maven central repository
    repositories {
        mavenCentral()
    }

    // Both Client and Server depend only on Ice JAR
    dependencies {

        // JUnit
        testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0-M1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0-M1'

        // Zero ICE
        implementation 'com.zeroc:ice:3.7.3'

        // SLF4J
        implementation 'org.slf4j:slf4j-api:1.7.30'
        implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'

        // Apache Commons Lang
        implementation 'org.apache.commons:commons-lang3:3.10'


    }

}
